apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getVersionCode = { name ->
    Integer code = 0
    name = name.replaceAll('v', '')
    String[] components = name.split("\\.")
    Integer i = components.size() - 1

    for (component in components) {
        code += Math.pow(1000, i) * (component as Integer)
        i--;
    }

    return code
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--tags', '--long'
        standardOutput = stdout
    }

    stdout = stdout.toString().trim()
    stdout = stdout.replaceAll(stdout.charAt(stdout.indexOf('-')).toString(), '.')

    return stdout[0..(stdout.lastIndexOf('.') - 1)]
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.explosion204.tabatatimer"
        minSdkVersion 21
        targetSdkVersion 30
        versionName getVersionName()
        versionCode getVersionCode(versionName)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    def lifecycle_version = "2.2.0"
    def room_version = "2.2.5"
    def dagger_version = "2.29.1"
    def material_version = "1.2.1"
    def nav_version = "2.3.1"
    def recyclerview_version = "1.1.0"
    def recyclerview_selection_version = "1.0.0"
    def viewpager2_version = "1.0.0"
    def preference_version = "1.1.1"


    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // CardView
    implementation "androidx.cardview:cardview:1.0.0"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.recyclerview:recyclerview-selection:$recyclerview_selection_version"

    // Material Design
    implementation "com.google.android.material:material:$material_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

    // Annotation processor
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Dagger Android
    api "com.google.dagger:dagger-android:$dagger_version"
    api "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Floating action button
    implementation 'com.github.clans:fab:1.6.4'

    // ViewPager 2
    implementation "androidx.viewpager2:viewpager2:$viewpager2_version"

    // CircleIndicator
    implementation 'me.relex:circleindicator:2.1.4'

    // Color Picker
    implementation 'petrov.kristiyan:colorpicker-library:1.1.10'

    // AndroidX Preferences
    implementation "androidx.preference:preference-ktx:$preference_version"
}